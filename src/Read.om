(* Ulm's Oberon Library
   Copyright (C) 1989-1994 by University of Ulm, SAI, D-89069 Ulm, Germany
   ----------------------------------------------------------------------------
   Ulm's Oberon Library is free software; you can redistribute it
   and/or modify it under the terms of the GNU Library General Public
   License as published by the Free Software Foundation; either version
   2 of the License, or (at your option) any later version.

   Ulm's Oberon Library is distributed in the hope that it will be
   useful, but WITHOUT ANY WARRANTY; without even the implied warranty
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Library General Public License for more details.

   You should have received a copy of the GNU Library General Public
   License along with this library; if not, write to the Free Software
   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
   ----------------------------------------------------------------------------
   E-mail contact: oberon@mathematik.uni-ulm.de
   ----------------------------------------------------------------------------
   $Id: Read.om,v 1.2 1995/04/04 13:57:58 borchert Exp $
   ----------------------------------------------------------------------------
   $Log: Read.om,v $
   Revision 1.2  1995/04/04  13:57:58  borchert
   bug fix: Read.FieldS accidently cut off parts of a field in some
            cases with intermediate white spaces

   Revision 1.1  1994/02/23  07:46:42  borchert
   Initial revision

   ----------------------------------------------------------------------------
   AFB 6/89
   ----------------------------------------------------------------------------
*)

MODULE Read;

   IMPORT ASCII, Scan, Sets, StreamDisciplines, Streams;

   PROCEDURE LongIntS(s: Streams.Stream; VAR longint: LONGINT);
   BEGIN
      Scan.S1(s, "%d", longint);
   END LongIntS;

   PROCEDURE ShortIntS(s: Streams.Stream; VAR shortint: SHORTINT);
      VAR long: LONGINT;
   BEGIN
      LongIntS(s, long);
      IF s.count = 1 THEN
	 IF (long >= MIN(SHORTINT)) & (long <= MAX(SHORTINT)) THEN
	    shortint := SHORT(SHORT(long));
	 ELSE
	    s.count := 0;
	 END;
      END;
   END ShortIntS;

   PROCEDURE IntS(s: Streams.Stream; VAR int: INTEGER);
      VAR long: LONGINT;
   BEGIN
      LongIntS(s, long);
      IF s.count = 1 THEN
	 IF (long >= MIN(INTEGER)) & (long <= MAX(INTEGER)) THEN
	    int := SHORT(long);
	 ELSE
	    s.count := 0;
	 END;
      END;
   END IntS;

   PROCEDURE LongRealS(s: Streams.Stream; VAR longreal: LONGREAL);
   BEGIN
      Scan.S1(s, "%f", longreal);
   END LongRealS;

   PROCEDURE RealS(s: Streams.Stream; VAR real: REAL);
      VAR
	 longreal: LONGREAL;
   BEGIN
      Scan.S1(s, "%f", real);
   END RealS;

   PROCEDURE CharS(s: Streams.Stream; VAR ch: CHAR);
   BEGIN
      IF ~Streams.ReadByte(s, ch) THEN
	 ch := 0X;
      END;
   END CharS;

   PROCEDURE ByteS(s: Streams.Stream; VAR byte: BYTE);
   BEGIN
      IF ~Streams.ReadByte(s, byte) THEN
	 byte := 0X;
      END;
   END ByteS;

   PROCEDURE LineS(s: Streams.Stream; VAR line: ARRAY OF CHAR);
      VAR
	 index: INTEGER;
	 ch: CHAR;
	 lineterm: StreamDisciplines.LineTerminator; lindex: INTEGER;
   BEGIN
      StreamDisciplines.GetLineTerm(s, lineterm);
      index := 0;
      WHILE Streams.ReadByte(s, ch) & (ch # lineterm[0]) DO
	 IF index+1 < LEN(line) THEN
	    line[index] := ch; INC(index);
	 END;
      END;
      IF ~s.eof & (ch = lineterm[0]) & (lineterm[0] # 0X) &
	    (lineterm[1] # 0X) THEN
	 (* skip rest of line terminator *)
	 lindex := 1;
	 WHILE (lindex < LEN(lineterm)) & (lineterm[lindex] # 0X) &
	       Streams.ReadByte(s, ch) & (ch = lineterm[lindex]) DO
	    INC(lindex);
	 END;
      END;
      s.count := index;
      line[index] := 0X;
   END LineS;

   PROCEDURE LnS(s: Streams.Stream);
      (* skip input until next ASCII.nl or end of file *)
      VAR
	 ch: CHAR;
	 lineterm: StreamDisciplines.LineTerminator; lindex: INTEGER;
   BEGIN
      StreamDisciplines.GetLineTerm(s, lineterm);
      WHILE Streams.ReadByte(s, ch) & (ch # lineterm[0]) DO END;
      IF ~s.eof & (ch = lineterm[0]) & (lineterm[1] # 0X) THEN
	 (* skip rest of line terminator *)
	 lindex := 1;
	 WHILE (lindex < LEN(lineterm)) & (lineterm[lindex] # 0X) &
	       Streams.ReadByte(s, ch) & (ch = lineterm[lindex]) DO
	    INC(lindex);
	 END;
      END;
   END LnS;

   PROCEDURE StringS(s: Streams.Stream; VAR string: ARRAY OF CHAR);
      VAR
	 index: INTEGER;
	 ch: CHAR;
	 more: BOOLEAN;
	 skip: BOOLEAN;
	 whitespace: Sets.CharSet;

   BEGIN
      StreamDisciplines.GetWhiteSpace(s, whitespace);
      index := 0; skip := TRUE; more := TRUE;
      WHILE more & Streams.ReadByte(s, ch) DO
	 IF Sets.CharIn(whitespace, ch) THEN
	    more := skip;
	 ELSIF index+1 < LEN(string) THEN
	    skip := FALSE;
	    string[index] := ch; INC(index);
	 ELSE
	    more := FALSE;
	 END;
      END;
      IF ~more & (s.eof OR ~Streams.Back(s)) THEN END;
      string[index] := 0X;
      s.count := index;
   END StringS;

   PROCEDURE FieldS(s: Streams.Stream; VAR string: ARRAY OF CHAR) : BOOLEAN;
      (* records are separated by line terminators and fields are
	 delimited either by field separators or line terminators;
	 surrounding white space of fields is to be skipped;
	 Field does not skip line terminators but returns FALSE
	 if there are no more fields on that line;
	 Field returns an empty string for empty fields;
	 exception: if the field separators are a subset of the
	 set of white space, then only non-empty fields will be returned;
	 Field returns FALSE on empty lines
      *)
      VAR
	 fieldseps: Sets.CharSet;
	 whitespace: Sets.CharSet;
	 lineterm: StreamDisciplines.LineTerminator;
	 index: INTEGER;    (* of string *)
	 ch: CHAR;          (* last character read *)
	 skip: BOOLEAN;     (* OK to skip field separator? *)
	 mskip: BOOLEAN;    (* OK to skip multiple field separators? *)
	 wskip: BOOLEAN;    (* OK to skip white space? *)
	 lastwhite: INTEGER; (* index of last white space char *)
	 emptyFieldPossible: BOOLEAN;
   BEGIN
      StreamDisciplines.GetFieldSepSet(s, fieldseps);
      StreamDisciplines.GetLineTerm(s, lineterm);
      StreamDisciplines.GetWhiteSpace(s, whitespace);
      mskip := Sets.Subset(fieldseps, whitespace);
      Sets.Difference(whitespace, fieldseps, whitespace);
      skip := TRUE; wskip := TRUE; lastwhite := -1; emptyFieldPossible := FALSE;
      index := 0;
      WHILE Streams.ReadByte(s, ch) & (ch # lineterm[0]) &
	    (skip OR ~Sets.CharIn(fieldseps, ch)) DO
	 IF Sets.CharIn(fieldseps, ch) THEN
	    emptyFieldPossible := ~mskip;
	 ELSIF (~wskip OR ~Sets.CharIn(whitespace, ch)) &
	       (index+1 < LEN(string)) THEN
	    wskip := FALSE;
	    IF Sets.CharIn(whitespace, ch) THEN
	       lastwhite := index;
	    ELSE
	       lastwhite := -1;
	    END;
	    string[index] := ch; INC(index); mskip := FALSE;
	 ELSIF (index > 0) & ~Sets.CharIn(whitespace, ch) THEN
	    lastwhite := -1;
	 END;
	 skip := mskip;
      END;
      IF s.eof OR ~Streams.Back(s) THEN END;
      IF lastwhite >= 0 THEN
	 string[lastwhite] := 0X;
      ELSE
	 string[index] := 0X;
      END;
      s.count := index;
      RETURN emptyFieldPossible OR (index > 0)
   END FieldS;

   PROCEDURE ShortInt(VAR shortint: SHORTINT);
   BEGIN
      ShortIntS(Streams.stdin, shortint);
   END ShortInt;

   PROCEDURE Int(VAR int: INTEGER);
   BEGIN
      IntS(Streams.stdin, int);
   END Int;

   PROCEDURE LongInt(VAR longint: LONGINT);
   BEGIN
      LongIntS(Streams.stdin, longint);
   END LongInt;

   PROCEDURE Real(VAR real: REAL);
   BEGIN
      RealS(Streams.stdin, real);
   END Real;

   PROCEDURE LongReal(VAR longreal: LONGREAL);
   BEGIN
      LongRealS(Streams.stdin, longreal);
   END LongReal;

   PROCEDURE Char(VAR ch: CHAR);
   BEGIN
      IF ~Streams.ReadByte(Streams.stdin, ch) THEN
	 ch := 0X;
      END;
   END Char;

   PROCEDURE Byte(VAR byte: BYTE);
   BEGIN
      IF ~Streams.ReadByte(Streams.stdin, byte) THEN
	 byte := 0X;
      END;
   END Byte;

   PROCEDURE Line(VAR line: ARRAY OF CHAR);
   BEGIN
      LineS(Streams.stdin, line);
   END Line;

   PROCEDURE Ln;
   BEGIN
      LnS(Streams.stdin);
   END Ln;

   PROCEDURE String(VAR string: ARRAY OF CHAR);
   BEGIN
      StringS(Streams.stdin, string);
   END String;

   PROCEDURE Field(VAR string: ARRAY OF CHAR) : BOOLEAN;
   BEGIN
      RETURN FieldS(Streams.stdin, string)
   END Field;

END Read.
