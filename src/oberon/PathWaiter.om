(* Ulm's Oberon Library
   Copyright (C) 1989-2005 by University of Ulm, SAI, D-89069 Ulm, Germany
   ----------------------------------------------------------------------------
   Ulm's Oberon Library is free software; you can redistribute it
   and/or modify it under the terms of the GNU Library General Public
   License as published by the Free Software Foundation; either version
   2 of the License, or (at your option) any later version.

   Ulm's Oberon Library is distributed in the hope that it will be
   useful, but WITHOUT ANY WARRANTY; without even the implied warranty
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Library General Public License for more details.

   You should have received a copy of the GNU Library General Public
   License along with this library; if not, write to the Free Software
   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
   ----------------------------------------------------------------------------
   E-mail contact: oberon@mathematik.uni-ulm.de
   ----------------------------------------------------------------------------
   $Id: PathWaiter.om,v 1.1 2005/02/16 14:00:02 borchert Exp borchert $
   ----------------------------------------------------------------------------
   $Log: PathWaiter.om,v $
   Revision 1.1  2005/02/16 14:00:02  borchert
   Initial revision

   ----------------------------------------------------------------------------
*)

MODULE PathWaiter;

   IMPORT Args := UnixArguments, Clocks, Conclusions, Conditions, Errors,
      LoaderErrors, Names, PathConditions, Paths, PersistentObjects,
      PrivateShards, Process, Read, RelatedEvents, RemoteCDBNames,
      RemoteCommandNodes, Services, Shards, Streams, Tasks, TimeConditions,
      Times, UnixFiles, UnixNames, Write;

   VAR
      lidType: Services.Type;
      errors: RelatedEvents.Object;
      auth: Shards.Lid;
      path: ARRAY 512 OF CHAR;
      timecond: Conditions.Condition;

   PROCEDURE CreateLid(VAR lid: Shards.Lid; filename: ARRAY OF CHAR);
      VAR
	 s: Streams.Stream;
   BEGIN
      IF ~UnixFiles.Open(s, filename, UnixFiles.read, Streams.onebuf,
			 errors) THEN
	 Conclusions.Conclude(errors, Errors.fatal, "");
      END;
      RelatedEvents.Forward(s, errors);
      IF ~PersistentObjects.GuardedRead(s, lidType, lid) THEN
	 Conclusions.Conclude(errors, Errors.fatal, filename);
      END;
   END CreateLid;

   PROCEDURE ProcessArgs;
      VAR
	 flag: CHAR;
	 filename: ARRAY 512 OF CHAR;
	 as: Streams.Stream;
	 seconds: INTEGER;
	 timeout: Times.Time;
   BEGIN
      auth := NIL; timecond := NIL;
      Args.Init("[-a auth] [-t timeout] path");
      WHILE Args.GetFlag(flag) DO
	 CASE flag OF
	 | "a":
	    Args.FetchString(filename);
	    CreateLid(auth, filename);
	 | "t":
	    Args.Fetch(as); Read.IntS(as, seconds);
	    IF as.count = 0 THEN Args.Usage END;
	    Times.CreateAndSet(timeout, Times.relative, 0, seconds, 0);
	    TimeConditions.Create(timecond, Clocks.system, timeout);
	 ELSE
	    Args.Usage;
	 END;
      END;
      Args.FetchString(path);
      Args.AllArgs;
   END ProcessArgs;

   PROCEDURE WaitFor(path: ARRAY OF CHAR);
      VAR
	 pathcond: PathConditions.Condition;
	 conditions: Conditions.ConditionSet;
	 selected: Conditions.ConditionSet;
	 node: Names.Node;
	 cmdname: ARRAY 128 OF CHAR;
   BEGIN
      Conditions.CreateSet(conditions);
      PathConditions.Create(pathcond, path, Names.root, auth);
      Conditions.Incl(conditions, pathcond);
      IF timecond # NIL THEN
	 Conditions.Incl(conditions, timecond);
      END;
      Tasks.Select(conditions, selected);
      IF Conditions.In(selected, pathcond) THEN
	 IF ~PathConditions.GetNode(pathcond, node, errors) THEN
	    Conclusions.Conclude(errors, Errors.fatal, path);
	 END;
      ELSE
	 Args.GetCommandName(cmdname);
	 Write.StringS(Streams.stderr, cmdname);
	 Write.LineS(Streams.stderr, ": timelimit exceeded");
	 Process.Exit(Process.indicateFailure);
      END;
   END WaitFor;

BEGIN
   Services.SeekType("Shards.Lid", lidType);
   NEW(errors); RelatedEvents.QueueEvents(errors);
   ProcessArgs;
   WaitFor(path);
   Process.TerminateSoftly;
END PathWaiter.
