(* Ulm's Oberon Library
   Copyright (C) 1989-2005 by University of Ulm, SAI, D-89069 Ulm, Germany
   ----------------------------------------------------------------------------
   Ulm's Oberon Library is free software; you can redistribute it
   and/or modify it under the terms of the GNU Library General Public
   License as published by the Free Software Foundation; either version
   2 of the License, or (at your option) any later version.

   Ulm's Oberon Library is distributed in the hope that it will be
   useful, but WITHOUT ANY WARRANTY; without even the implied warranty
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Library General Public License for more details.

   You should have received a copy of the GNU Library General Public
   License along with this library; if not, write to the Free Software
   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
   ----------------------------------------------------------------------------
   E-mail contact: oberon@mathematik.uni-ulm.de
   ----------------------------------------------------------------------------
   $Id: XWMProtocol.om,v 1.1 2005/04/10 13:18:21 borchert Exp $
   ----------------------------------------------------------------------------
   $Log: XWMProtocol.om,v $
   Revision 1.1  2005/04/10 13:18:21  borchert
   Initial revision

   ----------------------------------------------------------------------------
*)

MODULE XWMProtocolEvents;

   IMPORT Disciplines, Events, Streams, Types, XAtoms, XClientMessages,
      XDisplays, XResources;

   TYPE
      Event = POINTER TO EventRec;
      EventRec =
	 RECORD
	    (Events.EventRec)
	    win: XResources.Window;
	    protocol: XAtoms.Atom;
	    timestamp: Types.Int32;
	    data: Streams.Stream;
	 END;

   TYPE
      EventType = POINTER TO EventTypeRec;
      EventTypeRec =
	 RECORD
	    protocol: XAtoms.Atom;
	    eventType: Events.EventType;
	    next: EventType;
	 END;
      WindowDiscipline = POINTER TO WindowDisciplineRec;
      WindowDisciplineRec =
	 RECORD
	    (Disciplines.DisciplineRec)
	    types: EventType;
	 END;
   VAR
      wdid: Disciplines.Identifier;

   PROCEDURE HandleMessage(event: Events.Event);
      VAR
	 wd: WindowDiscipline;
	 e: Event;
	 type: EventType;
	 atomID: XDisplays.AtomID;
	 protocol: XAtoms.Atom;
	 timestamp: Types.Int32;
   BEGIN
      WITH event: XClientMessages.Event DO
	 IF ~Disciplines.Seek(event.window, wdid, wd) THEN
	    RETURN
	 END;
	 IF event.format # 32 THEN RETURN END;
	 Streams.SetPos(event.data, 0);
	 IF ~Streams.Read(event.data, atomID) OR
	       ~Streams.Read(event.data, timestamp) OR
	       ~XAtoms.LookupByID(XResources.GetDisplay(event.window),
		     atomID, protocol) THEN
	    RETURN
	 END;
	 Streams.SetPos(event.data, 0);
	 type := wd.types;
	 WHILE (type # NIL) & (type.protocol # protocol) DO
	    type := type.next;
	 END;
	 IF type = NIL THEN RETURN END;
	 NEW(e); e.type := type.eventType; e.message := "X WM protocol message";
	 e.win := event.window;
	 e.protocol := protocol;
	 e.timestamp := timestamp;
	 e.data := event.data;
	 Events.Raise(e);
      END;
   END HandleMessage;

   PROCEDURE GetDiscipline(win: XResources.Window;
                           VAR wd: WindowDiscipline);
      VAR
	 et: Events.EventType;
   BEGIN
      IF ~Disciplines.Seek(win, wdid, wd) THEN
	 NEW(wd); wd.id := wdid; wd.types := NIL;
	 XClientMessages.GetEventType(win, "WM_PROTOCOLS", et);
	 Events.Handler(et, HandleMessage);
	 Disciplines.Add(win, wd);
      END;
   END GetDiscipline;

   PROCEDURE GetEventType(win: XResources.Window;
			  protocol: ARRAY OF CHAR;
                          VAR eventType: Events.EventType);
      VAR
	 wd: WindowDiscipline;
	 t: EventType;
	 p: XAtoms.Atom;
   BEGIN
      IF XAtoms.LookupByName(XResources.GetDisplay(win), protocol,
	    (* onlyIfExists = *) FALSE, p) THEN
	 GetDiscipline(win, wd);
	 t := wd.types;
	 WHILE (t # NIL) & (t.protocol # p) DO
	    t := t.next;
	 END;
	 IF t = NIL THEN
	    NEW(t); t.protocol := p; Events.Define(t.eventType);
	    t.next := wd.types; wd.types := t;
	 END;
	 eventType := t.eventType;
      ELSE
	 eventType := NIL;
      END;
   END GetEventType;

BEGIN
   wdid := Disciplines.Unique();
END XWMProtocolEvents.
