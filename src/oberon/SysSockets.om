(* Ulm's Oberon Library
   Copyright (C) 1989-1994 by University of Ulm, SAI, D-89069 Ulm, Germany
   ----------------------------------------------------------------------------
   Ulm's Oberon Library is free software; you can redistribute it
   and/or modify it under the terms of the GNU Library General Public
   License as published by the Free Software Foundation; either version
   2 of the License, or (at your option) any later version.

   Ulm's Oberon Library is distributed in the hope that it will be
   useful, but WITHOUT ANY WARRANTY; without even the implied warranty
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Library General Public License for more details.

   You should have received a copy of the GNU Library General Public
   License along with this library; if not, write to the Free Software
   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
   ----------------------------------------------------------------------------
   E-mail contact: oberon@mathematik.uni-ulm.de
   ----------------------------------------------------------------------------
   $Id: SysSockets.om,v 1.3 1996/09/18 07:52:07 borchert Exp $
   ----------------------------------------------------------------------------
   $Log: SysSockets.om,v $
   Revision 1.3  1996/09/18  07:52:07  borchert
   bug fix: socket parameter was missing in UNIXCALL invocation
            of GetSockOpt and SetSockOpt

   Revision 1.2  1994/07/04  07:43:13  borchert
   GetSockOpt/SetSockOpt & Shutdown & UDP operations added

   Revision 1.1  1994/02/23  08:00:37  borchert
   Initial revision

   ----------------------------------------------------------------------------
   AFB 10/91
   ----------------------------------------------------------------------------
*)

MODULE SysSockets;

   IMPORT RelatedEvents, SYS := SYSTEM, Sys, SysErrors, SysTypes;

   TYPE
      Socket = SysTypes.File;

   CONST
      (* protocol families *)
      pfUNSPEC     = 0;
      afUNSPEC     = pfUNSPEC;
      pfUNIX       = 1;      (* Unix domain sockets *)
      afUNIX       = pfUNIX;
      pfLOCAL      = 1;      (* POSIX name for AF_UNIX *)
      afLOCAL      = pfLOCAL;
      pfINET       = 2;      (* Internet IP Protocol *)
      afINET       = pfINET;
      pfAX25       = 3;      (* Amateur Radio AX.25 *)
      afAX25       = pfAX25;
      pfIPX        = 4;      (* Novell IPX *)
      afIPX        = pfIPX;
      pfAPPLETALK  = 5;      (* AppleTalk DDP *)
      afAPPLETALK  = pfAPPLETALK;
      pfNETROM     = 6;      (* Amateur Radio NET/ROM *)
      afNETROM     = pfNETROM;
      pfBRIDGE     = 7;      (* Multiprotocol bridge *)
      afBRIDGE     = pfBRIDGE;
      pfATMPVC     = 8;      (* ATM PVCs *)
      afATMPVC     = pfATMPVC;
      pfX25        = 9;      (* Reserved for X.25 project *)
      afX25        = pfX25;
      pfINET6      = 10;     (* IP version 6 *)
      afINET6      = pfINET6;
      pfROSE       = 11;     (* Amateur Radio X.25 PLP *)
      afROSE       = pfROSE;
      pfDECnet     = 12;     (* Reserved for DECnet project *)
      afDECnet     = pfDECnet;
      pfNETBEUI    = 13;     (* Reserved for 802.2LLC project *)
      afNETBEUI    = pfNETBEUI;
      pfSECURITY   = 14;     (* Security callback pseudo AF *)
      afSECURITY   = pfSECURITY;
      pfKEY        = 15;     (* PF_KEY key management API *)
      afKEY        = pfKEY;
      pfNETLINK    = 16;
      afNETLINK    = pfNETLINK;
      pfROUTE      = pfNETLINK; (* Alias to emulate 4.4BSD *)
      afROUTE      = pfROUTE;
      pfPACKET     = 17;        (* Packet family *)
      afPACKET     = pfPACKET;
      pfASH        = 18;     (* Ash *)
      afASH        = pfASH;
      pfECONET     = 19;     (* Acorn Econet *)
      afECONET     = pfECONET;
      pfATMSVC     = 20;     (* ATM SVCs *)
      afATMSVC     = pfATMSVC;
      pfSNA        = 22;     (* Linux SNA Project (nutters!) *)
      afSNA        = pfSNA;
      pfIRDA       = 23;     (* IRDA sockets *)
      afIRDA       = pfIRDA;

      pfMAX        = 32;     (* For now.. *)
      afMAX        = pfMAX;


   CONST
      (* socket types *)
      stream    = 1;     (* stream socket *)
      dgram     = 2;     (* datagram socket *)
      raw       = 3;     (* raw-protocol interface *)
      rdm       = 4;     (* reliably-delivered message *)
      seqpacket = 5;     (* sequenced packet stream *)
      (* more linux specific options *)


   CONST
      (* socket message flags *)
      msgOutOfBand = 1;  (* process out-of-band data *)
      msgPeek = 2;       (* peek at incoming message *)
      msgDontRoute = 4;  (* send without using routing tables *)

      (* shutdown options *)
      shutReceive  = 0;
      shutSend = 1;
      shutBoth = 2;

   CONST
      (* socket options at socket level -- see <sys/socket.h> *)
      socketLevel = 1;          (* options for socket level *)
      optDebug = 1;             (* turn on debugging info recording *)
      optReuseAddr = 2;         (* allow local address reuse *)
      optType = 3;              (* get socket type *)
      optError = 4;             (* get error status and clear *)
      optDontRoute = 5;         (* just use interface addresses *)
      optBroadcast = 6;         (* permit sending of broadcast msgs *)
      optSndBuf = 7;            (* send buffer size *)
      optRcvBuf = 8;            (* receive buffer size *)
      optKeepAlive = 9;         (* keep connections alive *)
      optOOBinline = 10;        (* leave received OOB data in line *)
      optNoCheck = 11;          (* linux specific *)
      optPriority = 12;         (* linux specific *)
      optLinger = 13;           (* linger on close if data present *)
      optBSDCompat = 14;        (* linux specific *)
      optPasscred = 16;         (* linux specific *)
      optPeercred = 17;         (* linux specific *)
      optRcvLoWat = 18;         (* receive low-water mark *)
      optSndLoWat = 19;         (* send low-water mark *)
      optRcvTimeO = 20;         (* receive timeout *)
      optSndTimeO = 21;         (* send timeout *)

      (* optAcceptConn and optUseLoopBack are unsupported on linux *)


   TYPE
      LingerVal =
	 RECORD
	    onoff: INTEGER;
	    linger: SysTypes.Time;
	 END;
      BoolVal = RECORD bool: INTEGER END;

   CONST
      (* Constants for socketcall multiplexer *)
      syssocket = 1;		(* sys_socket(2) *)
      sysbind = 2;		(* sys_bind(2) *)
      sysconnect = 3;		(* sys_connect(2) *)
      syslisten = 4;		(* sys_listen(2) *)
      sysaccept = 5;		(* sys_accept(2) *)
      sysgetsockname = 6;	(* sys_getsockname(2) *)
      sysgetpeername = 7;	(* sys_getpeername(2) *)
      syssocketpair = 8;	(* sys_socketpair(2) *)
      syssend = 9;		(* sys_send(2) *)
      sysrecv = 10;		(* sys_recv(2) *)
      syssendto = 11;		(* sys_sendto(2) *)
      sysrecvfrom = 12;		(* sys_recvfrom(2) *)
      sysshutdown = 13;		(* sys_shutdown(2) *)
      syssetsockopt = 14;	(* sys_setsockopt(2) *)
      sysgetsockopt = 15;	(* sys_getsockopt(2) *)
      syssendmsg = 16;		(* sys_sendmsg(2) *)
      sysrecvmsg = 17;		(* sys_recvmsg(2) *)


   PROCEDURE Create(VAR socket: Socket;
		    domain, type, protocol: INTEGER;
		    errors: RelatedEvents.Object) : BOOLEAN;
      (* create a new socket;
	 domain: protocol family (eg pfUNIX or pfINET)
	 type: socket type (eg stream or dgram)
	 protocol is one of /etc/protocols (or of the corresponding yp map)
      *)
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := domain;
      param[1] := type;
      param[2] := protocol;
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, syssocket, SYS.ADR (param)) THEN
	 socket := d0;
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Create");
	 RETURN FALSE
      END;
   END Create;

   PROCEDURE Bind(s: Socket; name: ARRAY OF BYTE;
		  errors: RelatedEvents.Object) : BOOLEAN;
      (* assigns name to an unnamed socket;
	 name should be of the appropiate address type
      *)
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[2] := LEN (name);
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysbind, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Bind");
	 RETURN FALSE
      END;
   END Bind;

   PROCEDURE Bind3(s: Socket; name: ARRAY OF BYTE;
		   namelen: LONGINT;
		   errors: RelatedEvents.Object) : BOOLEAN;
      (* like Bind but namelen overrides LEN(name) *)
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[2] := namelen;
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysbind, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Bind3");
	 RETURN FALSE
      END;
   END Bind3;

   PROCEDURE Listen(s: Socket; backlog: INTEGER;
		    errors: RelatedEvents.Object) : BOOLEAN;
      (* prepares a socket for the acceptance of connections;
	 backlog specifies the maximal queue length
      *)
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := backlog;
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, syslisten, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Listen");
	 RETURN FALSE
      END;
   END Listen;

   PROCEDURE Accept(VAR fd: SysTypes.File;
		    s: Socket; VAR addr: ARRAY OF BYTE;
		    errors: RelatedEvents.Object) : BOOLEAN;
      (* waits for an incoming connection related to s *)
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
	 len: INTEGER;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (addr));
      param[2] := SYS.VAL (LONGINT, SYS.ADR (len));
      len := SHORT(LEN(addr));
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysaccept, SYS.ADR (param)) THEN
	 fd := d0;
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Accept");
	 RETURN FALSE
      END;
   END Accept;

   PROCEDURE Connect(s: Socket; name: ARRAY OF BYTE;
		     errors: RelatedEvents.Object) : BOOLEAN;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[2] := SHORT(LEN(name));
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysconnect, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Connect");
	 RETURN FALSE
      END;
   END Connect;

   PROCEDURE Connect3(s: Socket; name: ARRAY OF BYTE;
		      namelen: LONGINT;
		      errors: RelatedEvents.Object) : BOOLEAN;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[2] := SHORT(namelen);
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysconnect, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Connect3");
	 RETURN FALSE
      END;
   END Connect3;

   PROCEDURE GetName(s: Socket; VAR name: ARRAY OF BYTE;
		     errors: RelatedEvents.Object) : BOOLEAN;
      (* return the current name of the given socket *)
      VAR
	 d0, d1: INTEGER;
	 len: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[2] := SYS.VAL (LONGINT, SYS.ADR (len));
      len := SHORT(LEN(name));
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysgetsockname, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "GetName");
	 RETURN FALSE
      END;
   END GetName;

   PROCEDURE GetPeerName(s: Socket; VAR name: ARRAY OF BYTE;
			 errors: RelatedEvents.Object) : BOOLEAN;
      (* return the name of the peer connected to socket s *)
      VAR
	 d0, d1: INTEGER;
	 len: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[2] := SYS.VAL (LONGINT, SYS.ADR (len));
      len := SHORT(LEN(name));
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysgetpeername, SYS.ADR(param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "GetPeerName");
	 RETURN FALSE
      END;
   END GetPeerName;

   PROCEDURE GetSockOpt(s: Socket; level, optname: INTEGER;
			VAR optval: ARRAY OF BYTE;
			errors: RelatedEvents.Object) : BOOLEAN;
      VAR
	 d0, d1: INTEGER;
	 len: LONGINT;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := level;
      param[2] := optname;
      param[3] := SYS.VAL (LONGINT, SYS.ADR (optval));
      param[4] := SYS.VAL (LONGINT, SYS.ADR (len));
      len := LEN(optval);
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysgetsockopt, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "GetSockOpt");
	 RETURN FALSE
      END;
   END GetSockOpt;

   PROCEDURE SetSockOpt(s: Socket; level, optname: INTEGER;
			optval: ARRAY OF BYTE;
			errors: RelatedEvents.Object) : BOOLEAN;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := level;
      param[2] := optname;
      param[3] := SYS.VAL (LONGINT, SYS.ADR (optval));
      param[4] := LEN (optval);
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, syssetsockopt, SYS.ADR (param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "SetSockOpt");
	 RETURN FALSE
      END;
   END SetSockOpt;

   PROCEDURE Shutdown(s: Socket; how: INTEGER;
		      errors: RelatedEvents.Object) : BOOLEAN;
      (* how = shutReceive, shutSend or shutBoth *)
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := how;
      IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysshutdown, SYS.ADR(param)) THEN
	 RETURN TRUE
      ELSE
	 SysErrors.Raise(errors, d0, Sys.socketcall, "Shutdown");
	 RETURN FALSE
      END;
   END Shutdown;

   PROCEDURE Receive(s: Socket;
		     buf: SysTypes.Address; buflen: SysTypes.Count;
		     flags: INTEGER;
		     errors: RelatedEvents.Object;
		     retry: BOOLEAN; VAR interrupted: BOOLEAN) : SysTypes.Count;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, buf);
      param[2] := buflen;
      param[3] := flags;
      interrupted := FALSE;
      LOOP
	 IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysrecv, SYS.ADR(param)) THEN
	    RETURN d0
	 END;
	 IF d0 = SysErrors.intr THEN
	    interrupted := TRUE;
	 END;
	 IF (d0 # SysErrors.intr) OR ~retry THEN
	    SysErrors.Raise(errors, d0, Sys.socketcall, "Receive");
	    RETURN -1
	 END;
      END;
   END Receive;

   PROCEDURE ReceiveFrom(s: Socket;
		         buf: SysTypes.Address; buflen: SysTypes.Count;
			 flags: INTEGER;
			 VAR from: ARRAY OF BYTE;
			 VAR fromlen: LONGINT;
		         errors: RelatedEvents.Object;
			 retry: BOOLEAN; VAR interrupted: BOOLEAN) : LONGINT;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, buf);
      param[2] := buflen;
      param[3] := flags;
      param[4] := SYS.VAL (LONGINT, SYS.ADR (from));
      param[5] := SYS.VAL (LONGINT, SYS.ADR (fromlen));
      interrupted := FALSE;
      LOOP
	 fromlen := LEN(from);
	 IF SYS.UNIXCALL(Sys.socketcall, d0, d1, sysrecvfrom, SYS.ADR (param)) THEN
	    RETURN d0
	 END;
	 IF d0 = SysErrors.intr THEN
	    interrupted := TRUE;
	 END;
	 IF (d0 # SysErrors.intr) OR ~retry THEN
	    SysErrors.Raise(errors, d0, Sys.socketcall, "ReceiveFrom");
	    RETURN -1
	 END;
      END;
   END ReceiveFrom;

   PROCEDURE Send(s: Socket;
		  buf: SysTypes.Address; buflen: SysTypes.Count;
		  flags: INTEGER;
		  errors: RelatedEvents.Object;
		  retry: BOOLEAN; VAR interrupted: BOOLEAN) : BOOLEAN;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, buf);
      param[2] := buflen;
      param[3] := flags;
      interrupted := FALSE;
      LOOP
	 IF SYS.UNIXCALL(Sys.socketcall, d0, d1, syssend, SYS.ADR(param)) THEN
	    RETURN TRUE
	 END;
	 IF d0 = SysErrors.intr THEN
	    interrupted := TRUE;
	 END;
	 IF (d0 # SysErrors.intr) OR ~retry THEN
	    SysErrors.Raise(errors, d0, Sys.socketcall, "Send");
	    RETURN FALSE
	 END;
      END;
   END Send;

   PROCEDURE SendTo(s: Socket;
		    buf: SysTypes.Address; buflen: SysTypes.Count;
		    flags: INTEGER;
		    name: ARRAY OF BYTE; namelen: LONGINT;
		    errors: RelatedEvents.Object;
		    retry: BOOLEAN; VAR interrupted: BOOLEAN) : BOOLEAN;
      VAR
	 d0, d1: INTEGER;
         param : ARRAY 6 OF LONGINT;
   BEGIN
      param[0] := s;
      param[1] := SYS.VAL (LONGINT, buf);
      param[2] := buflen;
      param[3] := flags;
      param[4] := SYS.VAL (LONGINT, SYS.ADR (name));
      param[5] := namelen;
      interrupted := FALSE;
      LOOP
	 IF SYS.UNIXCALL(Sys.socketcall, d0, d1, syssendto, SYS.ADR(param)) THEN
	    RETURN TRUE
	 END;
	 IF d0 = SysErrors.intr THEN
	    interrupted := TRUE;
	 END;
	 IF (d0 # SysErrors.intr) OR ~retry THEN
	    SysErrors.Raise(errors, d0, Sys.socketcall, "SendTo");
	    RETURN FALSE
	 END;
      END;
   END SendTo;

END SysSockets.
